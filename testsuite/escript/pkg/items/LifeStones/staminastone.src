
////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Stamina Stone script
// Double clicking a Stamina Stone will use it.
// If it is not "linked" to someone, ie. never used before, it will be linked to the user and the
// user will get a target.
// If it has been "linked" then there is a check to verify if the user is the one linked to the
// stone. If so then the user will get a target. If not then the user will get an error message.
// If the user targets the stone her stamina will charge the stone. Her stamina will be drained
// until either she reaches 1 or the stone is fully charged. If she targets herself then stamina
// will be transferred from the stone to her until either her stamina is full or the stone is
// empty.
//
////////////////////////////////////////////////////////////////////////////////////////////////////////////

use uo;
use os;

// include "include/client";
include ":attributes:attributes";
// include "include/objtype";
// include "include/string";
// include "include/magicitems";
// include "include/utility";

program use_staminastone( who, stone )

  var what, mystamina, maxstamina, staminadif, amt, capacity;
  if ( !InInventory( who.backpack, stone ) )
    SendSysMessage( who, "The stamina stone has to be in your backpack to use it!",
                    color := 33 );
    return;
  endif
  if ( !ReserveItem( stone ) ) // If the item is in use already
    SendSysMessage( who, "You can't use that right now!", color := 33 );
    return;
  endif
  if ( !GetObjProperty( stone, "ownedby" ) ) // If the stone is not linked already
    Init( who, stone );
  endif
  if ( GetObjProperty( stone, "ownedby" ) != who.serial ) // The stone was linked to someone else
    SendSysMessage( who, "You can't use that stamina stone. It isn't yours!",
                    color := 33 );
    return;
  endif
  SendSysMessage( who, "Target the stone to transfer your stamina to it or",
                  color := 100 );
  SendSysMessage( who, "target yourself to transfer stored stamina from the stone to you",
                  color := 100 );
  what := Target( who );
  if ( !what ) // If she presses escape or targets nothing
    SendSysMessage( who, "Canceled." );
    return;
  endif
  capacity := GetObjProperty( stone, "MaxCapacity" ); // Get the stones Maximum cap for stamina
  amt := GetObjProperty( stone, "Amount" ); //  How much stamina is in the stone now
  mystamina := AP_GetVital( who, STAMINA ); // How much stamina does she have

  ///////////////////////////////////////////////////////////////////////////////////////////////////
  // Section where we decide what to do based on what was targetted by the Stamina Stone user      //
  ///////////////////////////////////////////////////////////////////////////////////////////////////

  // If she targets herself then transfer any stored stamina to her until either she is full or
  // the stone is empty
  if ( what == who )
    maxstamina := AP_GetVitalMaximumValue( who, STAMINA );
    if ( !amt ) // If the stone is bone dry of stamina
      SendSysMessage( who, "That stone has no stored stamina.", color := 666 );
      return;
    endif
    if ( maxstamina == mystamina ) // If she doesn't need any stamina
      SendSysMessage( who, "Your energy level is already at its peak.",
                      color := 666 );
      return;
    endif
    staminadif := maxstamina - mystamina; // How low is her stamina? staminadif contains that info
    if ( amt <= staminadif ) // If she needs more stamina than the stone has
      staminadif := amt; // just give her all the stamina left in the stone
    endif
    AP_SetVital( who, STAMINA, mystamina + staminadif ); // Transfer the stamina to her
    SendSysMessage( who, "Your mind feels stronger.", color := 68 );
    SetObjProperty( stone, "Amount", amt - staminadif ); // Take the stamina "out of" the stone
    PlaySoundEffect( who, 0x1FB ); // Play a cool sound effect

  // If she targets the stone then transfer all but 1 of her stamina to the stone
  elseif ( what == stone )
    if ( !GetObjProperty( stone, "Charges" ) ) // If this Property is 0 then the stone cannot be charged again
      SendSysMessage( who,
                      "The stone cannot recieve any more stamina. It has been used too many times.",
                      color := 33 );
      SendSysMessage( who,
                      "However there may still be more stamina you can recover from the stone.",
                      color := 44 );
      return; // Done
    endif

    // Then transfer stamina from her to the stone until the stone is full but
    // stamina transferred is not to exceed her total stamina -1 or the stones maximum capacity

    if ( amt == capacity ) // Is the stone already at max capacity?
      SendSysMessage( who, "That Stamina Stone cannot hold any more stamina.",
                      color := 44 );
    elseif ( mystamina < 2 ) // if her stamina is less than 2 there ain't none to transfer
      SendSysMessage( who, "You must rest before giving your stamina to the stone.",
                      color := 33 );
    else
      // Find out how much the stone needs to be fully charged - staminadif
      staminadif := capacity - amt;
      if ( !staminadif <= mystamina - 1 ) // If the stone needs more than her stamina - 1
        staminadif := mystamina - 1; // then give all her available stamina - 1
      endif
      if ( staminadif + amt >= capacity ) // If giving the stone all her stamina will put it over
        staminadif := capacity - amt; // its max cap then only give it what it needs.
      endif
      AP_SetVital( who, STAMINA, AP_GetVital( who, STAMINA ) - staminadif ); // Take the stamina from her
      SetObjProperty( stone, "Amount", amt + staminadif ); // Place it "in" the stone
      SetObjProperty( stone, "Charges", GetObjProperty( stone, "Charges" )
                    - 1 ); // Lose a charge
      SendSysMessage( who, "You transfer the stamina.", color := 68 );
      PlaySoundEffect( who, 0x1EC ); // Play a cool sound effect
    endif
  else // Well, she tried to target something other than herself.
    SendSysMessage( who,
                    "You can only use a Stamina Stone for tranferring stamina to or from yourself silly!",
                    color := 33 );
  endif
  Status( stone );
endprogram

////////////////////////////////////////////////////////////////////////////////////////////////////
// This function is called the first time a Stamina Stone is used to initialize the stone's CProps //
////////////////////////////////////////////////////////////////////////////////////////////////////
function Init( who, stone )

  SetObjProperty( stone, "MaxCharges", 10 );
  SetObjProperty( stone, "Charges", 10 );
  SetObjProperty( stone, "MaxCapacity", RandomInt( 400 ) + 91 );
  SetObjProperty( stone, "Amount", 0 );
  LinkTheItem( who, stone ); // This makes the stone only usable by the original user
endfunction

/////////////////////////////////////////////////////////////////////////////////////////////////////
// Links the item to the original user.                                                            //
// Customized "own" function for Life Stones so they are permanently attached to the original user //
// Yeah I ripped off the own command for this function.                                            //
/////////////////////////////////////////////////////////////////////////////////////////////////////

function LinkTheItem( who, it )

  //      Can only mark one stone at a time
  if ( it.amount > 1 )
    PrintTextAbovePrivate( it, "You can't mark those.", who );
    return;
  endif

  PrintTextAbovePrivate( it, "The stone is now linked to your mind. Only you can use it.",
                         who );
  PlaySoundEffect( who, 0x1e9 );
  var ownerserial := who.serial;
  var ownername := who.name;
  var thename := it.desc;
  SetObjProperty( it, "oldname", thename );
  var longname := SplitWords( thename );
  if ( longname[1] == "a" || longname[1] == "an" )
    thename := "";
    foreach word in longname
      if ( word != longname[1] )
        thename := thename + " " + word;
      endif
    endforeach
    it.name := who.name + "'s" + TheName;
  else
    it.name := who.name + "'s " + TheName;
  endif
  it.sellprice := -1;
  SetObjProperty( it, "ownedby", ownerserial );
  SetObjProperty( it, "ownername", ownername );
endfunction

////////////////////////////////////////////////////////////////////////////////////////////////////////
// This function will check the wear on the stone and also gauge how much stamina the stone has left  //
// Used for placing something on the stone so the user can have an idea of the status of the stone    //
// ItemID uses this data for verifying a quantifiable, though not specific, amount of energy in the   //
// stone.                                                                                             //
// This was a fun function to write - NOT! *grins*                                                    //
////////////////////////////////////////////////////////////////////////////////////////////////////////
function Status( stone )

  // var maxcharges := GetObjProperty(stone, "MaxCharges");
  var charges := GetObjProperty( stone, "Charges" );
  var capacity := GetObjProperty( stone, "MaxCapacity" );
  var amt := GetObjProperty( stone, "Amount" );
  var condition, quantity;

  var wear := CInt( charges / 2 ); // Var used to calculate wear
  var storage := CInt( amt * 100 / capacity ); // Var used to gauge the amt of life-energy stored in the stone (%).

  case ( wear )
    5: condition := "unused";
    4: condition := "hardly used";
    3: condition := "used";
    2: condition := "worn";
    1: condition := "badly worn";
    default: condition := "unknown";
  endcase
  // "stone seems to hold " ... " Stamina"
  if ( storage >= 85 )
    quantity := "a very large amount of";
  elseif ( storage >= 70 )
    quantity := "a large amount of";
  elseif ( storage >= 50 )
    quantity := "a good amount of";
  elseif ( storage >= 25 )
    quantity := "a fair amount of";
  elseif ( storage > 0 )
    quantity := "a small amount of";
  else
    quantity := "no";
  endif
  SetObjProperty( stone, "Quantity", quantity );
  SetObjProperty( stone, "Condition", condition );
endfunction

function InInventory( byref inventory, byref it )

  foreach item in EnumerateItemsInContainer( inventory )
    if ( item == it )
      return 1;
    endif
  endforeach

  return 0;

endfunction
