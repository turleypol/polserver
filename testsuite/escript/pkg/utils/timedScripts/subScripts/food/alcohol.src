/* $Id: alcohol.src 503 2006-06-29 15:14:30Z austinheilman $
 *
 */

use uo;
use os;
use util;

include ":timedScripts:timedScripts";

program TimedScript_Alcohol( params )

  var mobile := params[1];
  var cycle := 0;
  while ( 1 )
    cycle += 1;
    if ( cycle == 1 )
      SendSysMessage( mobile, "You feel no effects from the alcohol." );
    elseif ( cycle == 5 )
      SendSysMessage( mobile, "You think you might get drunk if you have anymore alcohol." );
    elseif ( cycle == 8 )
      SendSysMessage( mobile, "You begin to feel a little buzzed." );
    elseif ( cycle == 13 )
      SendSysMessage( mobile, "You become drunk and stumble about." );
    elseif ( cycle == 20 )
      SendSysMessage( mobile, "You begin to feel ill from the alcohol." );
    endif

    var roll := RandomInt( 10 );
    if ( cycle >= 13 )
      mobile.facing := RandomInt( 8 );
      if ( !RandomInt( 6 ) )
        PlaySoundEffect( mobile, ( !mobile.gender ? 0x41E : 0x30F ) );
        PrintTextAbove( mobile, "*Burps*" );
      else
        PlaySoundEffect( mobile, ( !mobile.gender ? 0x42F : 0x31F ) );
        PrintTextAbove( mobile, "*Hic*" );
      endif

      if ( roll < 7 )
        PerformAction( mobile, 0x20 );
      endif
    endif

    if ( cycle > 25 ) // Boy this person is wasted.
      PerformAction( mobile, 0x20 );
      if ( roll < 3 )
        PlaySoundEffect( mobile, ( !mobile.gender ? 0x440 : 0x32E ) );
        CreateItemAtLocation( mobile.x, mobile.y, mobile.z, "vomit", 1,
                              mobile.realm );
      endif
    endif

    Sleep( RandomInt( 4 ) + 3 );
  endwhile

  return 1;
endprogram
