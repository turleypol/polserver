use uo;
use os;
use polsys;
use file;

include "testutil";
const GREEN:="\x1b[32m";
const RED:="\x1b[31m";
const RESET:="\x1b[0m";

program tests()
  var test_count:=CInt(GetEnvironmentVariable("POLCORE_TEST_RUN"));
  syslog(
    "\n##########################\n"
    + "#  starting testscripts  #\n"
    + "#         Run: "+test_count+"         #\n"
    + "##########################\n",0);
  var exitcode:=0;
  var testpkgs:={};
  var restartpkg;
  foreach pkg in Packages()
    if (pkg.dir.find("testpkgs")==1)
      if (pkg.name == "testrestart")
        restartpkg:={pkg.name,pkg.desc};
      else
        testpkgs.append({pkg.name,pkg.desc});
      endif
    endif
  endforeach
  testpkgs.sort(1);
  // to prevent cleanup of other tests, TestRestart always at the end
  if (restartpkg)
    testpkgs.append(restartpkg);
  endif
  
  var filter:=GetEnvironmentVariable("POLCORE_TEST_FILTER");
  if (filter)
    syslog("## using testfilter \""+filter+"\"\n",0);
  endif

  foreach pkg in testpkgs
    if (filter and !pkg[1].find(filter))
      continue;
    endif
    
    if (test_count==2 and pkg[1]!="testrestart")
      continue;
    endif

    syslog(pkg[2],0);
    var scripts:=listdirectory(":{}:".format(pkg[1]),"ecl");
    scripts.sort();
    if ("setup.ecl" in scripts)
      syslog($"{GREEN}  Calling setup.ecl..{RESET}",0);
      var res:=run_script(":{}:setup.ecl".format(pkg[1]));
      if (res == IGNORE_TEST)
        continue;
      elseif (res != 1)
        syslog($"{RED}    failed: {res.errortext}{RESET}",0);
        exitcode:=1;
        continue;
      endif
    endif
    foreach file in scripts
      if (file.find("test") != 1)
        continue;
      endif
      syslog($"{GREEN}  Calling {file}..{RESET}",0);
      var script:=LoadExportedScript(":{}:{}".format(pkg[1], file));
      if (!script[2])
        syslog($"{RED}    failed: {script[2]}{RESET}",0);
        exitcode:=1;
        continue;
      endif
      if (script[2] == IGNORE_TEST)
        continue;
      endif
 
      foreach func in (script[1].exported_functions)
        syslog($"{GREEN}    Calling {func}..{RESET}",0);
        var res:=script[1].call(func);  
        if (res != 1)
          syslog($"{RED}      failed: {res.errortext}{RESET}",0);
          exitcode:=1;
          continue;
        endif
      endforeach
    endforeach
    if ("cleanup.ecl" in scripts)
      syslog($"{GREEN}  Calling cleanup.ecl..{RESET}",0);
      run_script(":{}:cleanup.ecl".format(pkg[1]));
    endif
  endforeach
  
  var result:="success";
  if (exitcode)
    result:="failed ";
  endif
  syslog(
    "\n###########################\n"
    + "# finished tests: {} #\n".format(result)
    + "###########################\n",0);
  shutdown(exitcode);
endprogram
